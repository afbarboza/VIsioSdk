plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.kotlinx.serialization.plugin)
    id("maven-publish")
}

android {
    namespace 'org.usp.barboza.visioaux'
    compileSdk 34

    defaultConfig {
        minSdk 21
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material

    /* VisioATF dependency */
    implementation(libs.atf.visio.aux)

    /* KotlinX serialization */
    implementation(libs.kotlinx.serialization)

    /* Retrofit HTTPS requests */
    implementation libs.retrofit
    implementation libs.okhttp
    implementation libs.logging.interceptor
    implementation libs.retrofit2.kotlinx.serialization.converter

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}

def homeDirectory = System.properties['user.home']
def localMavenDirectory = "/.m2/repository/" /* Change this if your local maven is in a different directory */
def visioSdkGroupId = 'org.usp.barboza.visioaux'
def visioSdkArtifactId = 'visio-sdk'
def versionName = "0.0.1-local"
def debugSuffix = "-debug"
def releaseSuffix = "-release"

publishing {
    publications {
        maven(MavenPublication) {
            //from components.android
            groupId visioSdkGroupId
            artifactId visioSdkArtifactId
            version versionName + debugSuffix

            afterEvaluate {
                from components.debug
            }
        }
    }

    repositories {
        maven {
            url homeDirectory + localMavenDirectory
        }
    }
}